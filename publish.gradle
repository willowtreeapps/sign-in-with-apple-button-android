apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'


group = 'com.github.khaledshamieh'
version = '0.4'

def siteUrl = 'https://github.com/KhaledShamieh/sign-in-with-apple-button-android'
def gitUrl = 'https://github.com/KhaledShamieh/sign-in-with-apple-button-android.git'

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from(android.sourceSets.main.java.srcDirs)
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError = false
}

task javadocsJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from(androidJavadocs)
}


publishing {
    publications {
        Production(MavenPublication) {
            artifact sourcesJar
            artifact javadocsJar
            artifact("$buildDir/outputs/aar/signIn-with-apple-release.aar")
            groupId this.group
            artifactId 'signIn-with-apple'
            version this.version

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}
bintray {
    // Get Bintray credential from environment variable
    user = System.getenv('BINTRAY_USER') // Get bintray User
    key = System.getenv('BINTRAY_KEY') // Get bintray Secret Key
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'signIn-with-apple'
        desc = 'Add a Sign In button for Sign In With Apple'

        websiteUrl = siteUrl
        vcsUrl = gitUrl

        labels = ['android', 'signin', 'apple']
        publicDownloadNumbers = true

        licenses = ['MIT']
        publish = true

        version {
            name = this.version //Bintray logical version name
            desc = 'Enhance configuration & fetch id_token in addition to code'
            vcsTag = this.version
        }
    }
    publications = ['Production']

}
